precision mediump float;

uniform vec2 u_playerPos;
uniform float u_playerAngle;

uniform vec2 u_mapSize;
uniform float u_cellSize;

uniform float u_screenHeight;

uniform float u_halfFov;
uniform float u_projectionDistance;

uniform sampler2D u_map;

attribute vec2 a_position;

varying vec4 v_color;

const float STEP_SIZE = 1.0;
const int MAX_DISTANCE = 1000;

void main() {
  gl_Position = vec4(a_position, 0.0, 1.0);

  float rayAngle = u_playerAngle + a_position.x * u_halfFov;

  vec2 dir = vec2(cos(rayAngle), sin(rayAngle));
  vec2 step = dir * STEP_SIZE;
  vec2 pos = u_playerPos + step;

  for (int i = 0; i < MAX_DISTANCE; i++) {
    vec2 texPos = pos / u_cellSize / u_mapSize;
    vec4 color = texture2D(u_map, texPos);
    if (color.r > 0.5) {
      float distance =
        sin(u_playerAngle) * (pos.y - u_playerPos.y) +
        cos(u_playerAngle) * (pos.x - u_playerPos.x);
      float height = u_cellSize / distance * u_projectionDistance / u_screenHeight;
      vec2 vertexPos = vec2(a_position.x, height * 0.5 * sign(a_position.y));
      gl_Position = vec4(vertexPos, 0.0, 1.0);
      v_color = vec4(color.rgb, 1);
      return;
    }
    pos += step;
  }
}
